plugins {
    id 'java'
    id 'maven-publish'
    id "net.kyori.blossom" version "1.1.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '0.0.4'

def ENV = System.getenv()
version = version + (ENV.GITHUB_ACTIONS ? "" : "+local")

group = 'org.quiltmc'
archivesBaseName = 'cfr'

sourceSets {
    main {
        java.srcDirs = ['src', 'src-templates']
    }
}

java {
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    // Target JDK 8 - yes CFR is java 6 but --release does not go that low
    if (JavaVersion.current().isJava9Compatible()) {
         it.options.release = 8
    }
}

blossom {
    replaceTokenIn 'src-templates/org/benf/cfr/reader/util/CfrVersionInfo.java'
    replaceToken '${project.version}', project.version
    replaceToken '${git.commit.id.abbrev}', 'git rev-parse --short HEAD'.execute().text.readLines()[0]
    replaceToken '${git.dirty}', 'git diff --quiet HEAD'.execute().waitFor() == 1
}

jar {
    manifest {
        attributes 'Main-Class': 'org.benf.cfr.reader.Main'
		attributes 'Implementation-Version': archiveVersion
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        if (ENV.MAVEN_URL) {
            maven {
                url ENV.MAVEN_URL
                credentials {
                    username ENV.MAVEN_USERNAME
                    password ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}
